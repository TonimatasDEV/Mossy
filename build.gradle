//file:noinspection VulnerableLibrariesLocal

import dev.tonimatas.tasks.Utils
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id("java")
    id("com.github.johnrengelman.shadow") version "7.1.0"
}

group = "dev.tonimatas"
version = "1.0.0"
base.archivesName.set("mossy")

configurations {
    libraries
}

repositories {
    mavenCentral()
    maven{url = "https://jitpack.io/"}
}

dependencies {
    implementation("net.minestom:minestom-snapshots:dcbdad9a1b")
    implementation("org.jetbrains:annotations:24.0.1")
}

jar {
    from(rootProject.file("data/libraries.txt")) {
        filter(ReplaceTokens, tokens: [LIBRARIES: Utils.getDependencies(rootProject.configurations.implementation.copy()).join("\n")])
    }

    from(rootProject.file("data/repositories.txt")) {
        filter(ReplaceTokens, tokens: [REPOSITORIES: Utils.getRepositories(repositories).join("\n")])
    }

    manifest {
        attributes(
            "Launcher-Agent-Class": "dev.tonimatas.mossy.launcher.Agent",
            "Agent-Class": "dev.tonimatas.mossy.launcher.Agent",
            "Premain-Class": "dev.tonimatas.mossy.launcher.Agent",
            "Main-Class": "dev.tonimatas.mossy.launcher.Main",
            "Multi-Release": true
        )
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
